let u = import("./utils.gr")
let html = import("./html.gr")

fn generate_feed(posts) {
	let renderer = html.Renderer()
	renderer.renderXML = true
	posts = u.sortBy(posts, fn (a, b) {
		if a["date"] < b["date"] {
			return 1
		} elif a["date"] > b["date"] {
			return -1
		} else {
			return 0
		}
	})
	let content = ["feed", ["xmlns", "http://www.w3.org/2005/Atom"], [
		["generator", ["uri", "https://github.com/mliezun/mliezun.github.io", "version", "20240110"], [
			"mliezun.com"
		]],
		["link", ["href", "https://mliezun.com/feed.xml", "rel", "self", "type", "application/atom+xml"]],
		["link", ["href", "https://mliezun.com/", "rel", "alternate", "type", "text/html"]],
		["id", [], ["https://mliezun.com/"]],
		["title", [], ["mliezun.com"]],
		["updated", [], [posts[0]["date"] + "T00:00:00Z"]],
		["subtitle", [], ["I'm Miguel. Here I write mainly about programming and side projects."]],
	]]
	let entries = u.map(posts, fn (p) {
		let post_obj = p["post_obj"]
		let url = "https://mliezun.com" + p["url"]
		let date = p["date"]
		let content = renderer.render(post_obj.content)
		let replacements = {
			">": "&gt;",
			"<": "&lt;",
			"\"": "&quot;",
		}
		for r, n in replacements {
			content = u.replace(content, r, n)
		}
		return ["entry", [], [
			["title", ["type", "html"], [post_obj.title]],
			["id", [], [url]],
			["updated", [], [date + "T00:00:00Z"]],
			["link", ["href", url, "rel", "alternate", "type", "text/html"]],
			["content", ["type", "html", "xml:base", url], content],
			["author", [], [
				["name", [], "Miguel Liezun"]
			]],
			["category", ["term", "posts"]],
			["summary", ["type", "html"], [post_obj.excerpt]]
		]]
	})
	content[2] = content[2] + entries
	let text = renderer.render([content])
	let replacements = {
		"<>": "&lt;&gt;",
		"<=": "&lt;=",
		">=": "&gt;=",
		"\">\"": "\"&gt;\"",
		"\">=\"": "\"&gt;=\"",
		"\"<\"": "\"&lt;\"",
		"\"<=\"": "\"&lt;=\"",
		"->": "-&gt;",
		"<-": "-&lt;",
		" >": " &gt;",
		"< ": "&lt; ",
	}
	for r, n in replacements {
		text = u.replace(text, r, n)
	}
	return text
}
