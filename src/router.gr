let u = import("./utils.gr")
let static = import("./static.gr")
let md = import("./parse_md.gr")
let pages = import("./pages/base.gr")

fn get_routes() {
    # Store routes here
    let routes = {}

    # Add static folders
    let statics = static.build("/assets", "./assets")
    for url, content in statics {
        routes[url] = {
            "class": "static",
            "content": content,
        }
    }


    # Store post dicts here, in the form {"post_ojb":..., "url": ...}
    let posts = []

    # List of directories for posts
    let postList = io.listDir("src/pages/posts")

    # Compute urls and get objects for posts
    u.each(u.map(postList, fn (p) {
        let name = p["name"]
        return {
            "url": "/" + name[0:4] + "/" + name[5:7] + "/" + name[8:10] + "/" + name[11:name.length-3] + ".html",
            "date": p["name"][:10],
            "post_obj": import("pages/posts/" + name).post,
        }
    }), fn (r) {
        let p = r["post_obj"]
        routes[r["url"]] = {
            "class": "html",
            "content": p.content,
            "metadata": {
                "date": r["date"],
                "excerpt": p.excerpt,
                "title": p.title,
                "author": p.author,
                "tags": p.tags,
                "content_type": "article",
            }
        }
        posts = posts + [r]
    })

    # List markdown files and append to post list
    let mdPostList = io.listDir("src/pages/md")
    u.each(u.map(mdPostList, fn (p) {
        let name = p["name"]
        let url = "/" + name[0:4] + "/" + name[5:7] + "/" + name[8:10] + "/" + name[11:name.length-3] + ".html"
        let date = p["name"][:10]
        let content = io.readFile("src/pages/md/" + name)
        let parsed = md.parseHeader(content)
        let body = md.parseBody(parsed[0])
        let header = parsed[1]
        return {
            "url": url,
            "post": {
                "class": "html",
                "content": body,
                "metadata": {
                    "date": date,
                    "excerpt": header["excerpt"],
                    "title": header["title"],
                    "author": header["author"],
                    "tags": header["tags"],
                    "content_type": "article",
                }
            }
        }
    }), fn (r) {
        let p = {
            "url": r["url"],
            "date": r["post"]["metadata"]["date"],
            "post_obj": pages.Post(
                r["post"]["metadata"]["title"],
                r["post"]["metadata"]["excerpt"],
                r["post"]["metadata"]["author"],
                r["post"]["metadata"]["tags"],
                r["post"]["content"],
            ),
        }
        r["post"]["content"] = p["post_obj"].content
        routes[r["url"]] = r["post"]
        posts = posts + [p]
    })

    posts = u.sortBy(posts, fn (pa, pb) {
        return strings.compare(pa["url"], pb["url"])
    })

    # Build index using posts dicts
    routes["/"] = {
        "class": "html",
        "content": import("pages/index.gr").build_index(posts),
        "metadata": {
            "title": "Blog | mliezun.github.io",
            "author": "Miguel Liezun",
            "tags": "software,tech,fun",
            "date": "",
            "excerpt": "Blog about programming and side projects",
            "content_type": nil
        }
    }
    
    return routes
}
