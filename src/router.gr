let u = import("./utils.gr")
let static = import("./static.gr")
let md = import("./parse_md.gr")
let pages = import("./pages/base.gr")
let feed = import("./feed.gr")

fn get_routes(include_feed) {
    # Store routes here
    let routes = {}

    # Add static folders
    let statics = static.build("/assets", "./assets")
    for url, content in statics {
        routes[url] = {
            "class": "static",
            "content": content,
        }
    }


    # Store post dicts here, in the form {"post_ojb":..., "url": ...}
    let posts = []

    # List of directories for posts
    let postList = io.listDir("src/pages/posts")

    let defaultImage = "https://mliezun.com/assets/images/nyc.jpg"

    # Compute urls and get objects for posts
    u.each(u.map(postList, fn (p) {
        let name = p["name"]
        return {
            "url": "/" + name[0:4] + "/" + name[5:7] + "/" + name[8:10] + "/" + name[11:name.length-3] + ".html",
            "date": p["name"][:10],
            "post_obj": import("pages/posts/" + name).post,
        }
    }), fn (r) {
        let p = r["post_obj"]
        routes[r["url"]] = {
            "class": "html",
            "content": p.content,
            "metadata": {
                "date": r["date"],
                "excerpt": p.excerpt,
                "title": p.title,
                "author": p.author,
                "tags": p.tags,
                "image": defaultImage,
                "content_type": "article",
            }
        }
        posts = posts + [r]
    })

    # List markdown files and append to post list
    let mdPostList = io.listDir("src/pages/md")
    u.each(u.map(mdPostList, fn (p) {
        let name = p["name"]
        let url = "/" + name[0:4] + "/" + name[5:7] + "/" + name[8:10] + "/" + name[11:name.length-3] + ".html"
        let date = p["name"][:10]
        let content = io.readFile("src/pages/md/" + name)
        let parsed = md.parseHeader(content)
        let replaceSpecials = false
        if date > "2025-03-20" {
            replaceSpecials = true
        }
        let body = md.parseBody(parsed[0], replaceSpecials)
        let header = parsed[1]
        let image = u.get(header, "image", defaultImage)
        let aiAssisted = u.get(header, "ai-assisted", "false")
        # Convert string to boolean
        let aiAssistedBool = (aiAssisted == "true")
        return {
            "url": url,
            "post": {
                "class": "html",
                "content": body,
                "metadata": {
                    "date": date,
                    "excerpt": u.get(header, "excerpt", ""),
                    "title": u.get(header, "title", ""),
                    "author": u.get(header, "author", ""),
                    "tags": u.get(header, "tags", ""),
                    "image": image,
                    "ai-assisted": aiAssistedBool,
                    "content_type": "article",
                }
            }
        }
    }), fn (r) {
        let p = {
            "url": r["url"],
            "date": r["post"]["metadata"]["date"],
            "ai-assisted": r["post"]["metadata"]["ai-assisted"],
            "post_obj": pages.Post(
                r["post"]["metadata"]["title"],
                r["post"]["metadata"]["excerpt"],
                r["post"]["metadata"]["author"],
                r["post"]["metadata"]["tags"],
                r["post"]["content"],
            ).setImage(r["post"]["metadata"]["image"]),
        }
        r["post"]["content"] = p["post_obj"].content
        routes[r["url"]] = r["post"]
        posts = posts + [p]
    })

    posts = u.sortBy(posts, fn (pa, pb) {
        return strings.compare(pa["url"], pb["url"])
    })

    if include_feed {
        # Build rss feed
        routes["/feed.xml"] = {
            "class": "static",
            "content": feed.generate_feed(posts),
        }
    }

    # Build index using posts dicts
    routes["/"] = {
        "class": "html",
        "content": import("pages/index.gr").build_index(posts),
        "metadata": {
            "title": "Miguel's dev blog",
            "author": "Miguel Liezun",
            "tags": "software,tech,fun",
            "date": "",
            "excerpt": "I'm Miguel. Here I write mainly about programming and side projects.",
            "image": defaultImage,
            "content_type": nil
        }
    }

    routes["/now/index.html"] = {
        "class": "html",
        "content": import("pages/now.gr").build_now(),
        "metadata": {
            "title": "Now page | mliezun.com",
            "author": "Miguel Liezun",
            "tags": "software,tech,fun",
            "date": "2024-07-17",
            "excerpt": "Now page for my personal blog.",
            "image": defaultImage,
            "content_type": nil
        }
    }
    
    return routes
}
