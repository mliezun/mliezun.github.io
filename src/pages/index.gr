let u = import("../utils.gr")


fn aboutme() {
    return ["div", ["class", "aboutme"], [
        ["p", [], "Hi folks!"],
        ["p", [], "I'm Miguel. Here I write mainly about programming and side projects."],
        ["p", [], [
            "I've written my own programming language called Grotsky, and it's ",
            ["a", ["href", "https://mliezun.com/2023/11/23/grotsky-rust-part3.html", "target", "_blank"], "implemented in Rust"],
            ". This blog's engine is written with Grotksy and generates static HTML files."
        ]]
    ]]
}

fn group_by_year(posts) {
    let years = {}
    # Fill years with empty lists
    for post in posts {
        let year = strings.split(post["date"], "-")[0]
        years[year] = []
    }
    # Group by years now
    for post in posts {
        let year = strings.split(post["date"], "-")[0]
        years[year] = years[year] + [post]
    }
    return years
}

fn build_index(posts) {
    let years = group_by_year(posts)
    let years_list = u.keys(years)
    years_list = u.sortBy(years_list, fn (a, b) strings.compare(a, b))
    
    return [
        aboutme(),
        [
            "div", [],
            u.foldr(u.map(years_list, fn (year) {
                return [
                    # Year heading
                    ["h2", ["style", "font-size: 20pt; font-weight: bold; margin-top: 40px; margin-bottom: 20px; color: var(--text-heading);"], year],
                    # Posts for this year
                    ["div", [],
                        u.foldr(u.map(years[year], fn (postDict) {
                            let p = postDict["post_obj"]
                            let url = postDict["url"]
                            return [[
                                "div", ["style", "margin-bottom: 16px;"], [
                                    ["a", ["href", url, "style", "font-size: 13pt; text-decoration: none; color: var(--text-link); transition: color 0.2s ease; letter-spacing: -0.015em; line-height: 1.4;"], p.title],
                                    ["span", ["style", "color: var(--text-secondary); margin-left: 8px; font-size: 10pt; letter-spacing: 0;"], [
                                        ["span", ["style", "margin: 0 5px;"], " "],
                                        postDict["date"]
                                    ]]
                                ]
                            ]]
                        }), [], u.sum)
                    ]
                ]
            }), [], u.sum)
        ]
    ]
}
